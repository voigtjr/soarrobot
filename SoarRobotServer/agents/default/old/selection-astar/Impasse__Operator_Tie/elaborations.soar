###
###      7.3.3.3 Computing symbolic evaluations
###
### Create pick-best-numeric-evaluation operator in selection problem space.

# Propose after evaluations are done
# replaces original rule
sp {selection*compare*success-evaluation-better-than-partial-success
   :default
   (state <s> ^name selection
              ^superstate <ss>
              ^evaluation <e1> { <> <e1> <e2> })
  -{(<s> ^operator <o> +)
    (<o> ^name evaluate-operator)}

   (<ss> ^desired <d>)
   (<e1> ^superoperator <x>
         ^symbolic-value partial-success
         ^success-depth <n>
         ^desired <d>)
   (<e2> ^superoperator { <> <x> <y> }
         ^symbolic-value partial-success
         ^success-depth > <n>
         ^desired <d>)
-->
   (<ss> ^operator <x> > <y>)}

# Replaces original rule
sp {selection*compare*success-evaluation-equal-than-partial-success
   :default
   (state <s> ^name selection
              ^superstate <ss>
              ^evaluation <e1> { <> <e1> <e2> })
  -{(<s> ^operator <o> +)
    (<o> ^name evaluate-operator)}
   (<ss> ^desired <d>)
   (<e1> ^superoperator <x>
         ^symbolic-value partial-success
         ^success-depth <n>
         ^desired <d>)
   (<e2> ^superoperator { <> <x> <y> }
         ^symbolic-value partial-success
         ^success-depth <n>
         ^desired <d>)
-->
   (<ss> ^operator <x> = <y>)}

sp {selection*compare*same-symbolic-evaluations-are-indifferent
   :default
   (state <s> ^name selection
              ^superstate <ss>
              ^evaluation <e1>  { <> <e1> <e2> })
   (<ss> ^desired <d>)
   (<e1> ^superoperator <x>
         ^symbolic-value { <> partial-success <v> }
         ^desired <d>)
   (<e2> ^superoperator <y>
         ^symbolic-value { <> partial-success <v> }
         ^desired <d>)
-->
   (<ss> ^operator <x> = <y>)}


sp {selection*compare*partial-success-evaluation-better-than-failure
   :default
   (state <s> ^name selection
              ^superstate <ss>
              ^evaluation <e1> { <> <e1> <e2> })
   (<ss> ^desired <d>)
   (<e1> ^superoperator <x>
         ^symbolic-value partial-success
         ^desired <d>)
   (<e2> ^superoperator { <> <x> <y> }
         ^symbolic-value << failure partial-failure >>
         ^desired <d>)
-->
   (<ss> ^operator <x> > <y>)}

sp {selection*select*success-evaluation-becomes-best-preference
   :default
   (state <s> ^name selection
              ^superstate <ss>
              ^evaluation <e1>)
  -{(<s> ^operator <o> +)
    (<o> ^name evaluate-operator)}
   (<ss> ^desired <eb>)
   (<e1> ^symbolic-value << partial-success success >>
         ^desired <eb>
         ^superoperator <o1>)
-->
   (<ss> ^operator <o1> >)}

