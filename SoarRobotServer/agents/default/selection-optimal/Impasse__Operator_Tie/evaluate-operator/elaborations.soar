############## Rules that force optimal solution

sp {evaluate-operator*elaborate*symbolic-depth*from-subgoal*new
   :default
   (state <ss> ^superstate <s>
               ^success-depth <sd>)
   (<s> ^operator <so>
        ^evaluation <e>)
   (<so> ^name evaluate-operator
         ^evaluation <e>
         ^evaluation.desired <eb>)
-->
   (<e> ^success-depth <sd>)}

sp {selection*compute*failure-evaluation-exceed-cutoff*new
   :default
   (state <task-g> ^depth <depth>
                   ^quiescence t
                   ^desired <d>
                   ^superstate <eval-s>)
   (<eval-s> ^evaluation.success-depth { <sdepth> <= <depth> })
-->
   (write (crlf) |Exceeded success depth: | <sdepth> | with depth | <depth>)
   (<task-g> ^partial-failure <d>)}