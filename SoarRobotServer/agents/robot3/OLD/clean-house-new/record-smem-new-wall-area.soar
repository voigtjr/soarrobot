sp {go-to*elaborate*set-identifier*wall
   :o-support
   (state <s> ^superstate nil
              ^current-location <ca>)
   (<ca> ^wall <wall> ^id <cid>)
   (<wall> -^id <id>
            ^open true
            ^x <x>
            ^y <y>)
-->
   (<wall> ^id (int (+ <cid> (+ 50000 (+ <x> <y>)))))
}

sp {clean-house*propose*record-smem-new-wall-area
   (state <s> ^name robot
              ^parameters.areas-held-in smem
              ^current-location <ca>
              ^io.input-link.self.area <area-id>)
   (<ca> ^id <area-id>
         ^wall <wall>)
   (<wall> ^connected no)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name record-smem-new-wall-area
         ^wall <wall>)
}
sp {apply*record-smem-new-wall*query
   (state <s> ^smem.command <cmd>
              ^operator <o>)
   (<o> ^name record-smem-new-wall-area
        ^wall.to <area-id>)
-->
   (<cmd> ^query <q>)
   (<q> ^type room
        ^id <area-id>)
}

sp {apply*record-smem-new-wall*retrieve
   (state <s> ^operator <o>
              ^current-location <c-area>
              ^smem <smem>)
   (<smem> ^command <cmd>
           ^result <res>)
   (<cmd> ^query <q>)
   (<q> ^type <type>
        ^id <id>)
   (<res> ^retrieved <cp>
          ^success <q>)
   (<cp> ^type <type>
         ^id <id>)
   (<o> ^name record-smem-new-wall-area
        ^wall <wall>)
-->
###(write (crlf) |New wall - create neighbor-area: | <cp>  | for area | <c-area> | apply*record-smem-new-wall*retrieve |)
###(write (crlf) | Store area: | <c-area> | neighbor | <cp> |, wall: | <wall>)
   (<wall> ^connected no -
                      yes)
   (<c-area> ^neighbor-area <cp>)
   (<cp> ^neighbor-area <c-area>)
   (<cmd> ^store <c-area> <cp> <wall>)}

sp {apply*record-smem-new-wall*retrieve*fail
#  :interrupt
   (state <s> ^operator <o>
              ^current-location <c-area>
              ^smem <smem>)
   (<smem> ^command <cmd>
           ^result <res>)
   (<cmd> ^query <q>)
   (<res> ^failure <q>)
   (<o> ^name record-smem-new-wall-area
        ^wall <wall>)
   (<wall> ^to <to>
           ^x <x>
           ^y <y>)
-->
###(write (crlf) |New wall - create neighbor-area: | <cp> | id | <to> | for area | <c-area> | apply*record-smem-new-wall*retrieve*fail*room |)
###(write (crlf) | Store area: | <c-area> | neighbor | <cp> |, wall: | <wall>)
   (<wall> ^connected no -
                      yes)
   (<c-area> ^neighbor-area <cp>)
   (<cp> ^neighbor-area <c-area>
         ^temp-x <x>   ## JEL
         ^temp-y <y>
         ^type room
         ^cleaned no 
         ^visited no
         ^id <to>)
   (<cmd> ^store <c-area> <cp> <wall>)}