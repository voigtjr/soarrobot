sp {clean-house*propose*record-smem-new-gateway-area
   (state <s> ^name robot
              ^parameters.areas-held-in smem
              ^current-location <ca>
              ^io.input-link.self.area <area-id>)
   (<ca> ^id <area-id>
         ^gateway <gateway>)
   (<gateway> ^connected no)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name record-smem-new-gateway-area
         ^gateway <gateway>)
}
sp {apply*record-smem-new-gateway*query*room
   (state <s> ^smem.command <cmd>
              ^io.input-link.area-description <area-d>
              ^operator <o>)
   (<o> ^name record-smem-new-gateway-area
        ^gateway.to <area-id>)
   (<area-d> ^type door)
-->
##(write (crlf) |Query Room - new gateway: | <q> |: id: | <area-id>)
   (<cmd> ^query <q>)
   (<q> ^type room
        ^id <area-id>)
}
sp {apply*record-smem-new-gateway*query*door
   (state <s> ^smem.command <cmd>
              ^io.input-link.area-description <area-d>
              ^operator <o>)
   (<o> ^name record-smem-new-gateway-area
        ^gateway.to <area-id>)
   (<area-d> ^type room)
-->
##(write (crlf) |Query door - new gateway: | <q> |: id: | <area-id>)
   (<cmd> ^query <q>)
   (<q> ^type door
        ^id <area-id>)
}

sp {apply*record-smem-new-gateway*retrieve
   (state <s> ^operator <o>
              ^current-location <c-area>
              ^smem <smem>)
   (<smem> ^command <cmd>
           ^result <res>)
   (<cmd> ^query <q>)
   (<q> ^type <type>
        ^id <id>)
   (<res> ^retrieved <cp>
          ^success <q>)
   (<cp> ^type <type>
         ^id <id>)
   (<o> ^name record-smem-new-gateway-area
        ^gateway <gateway>)
   (<c-area> ^id <cid>)
-->
###(write (crlf) |New gateway - retrieve neighbor-area: | <cp> | id: | <id> | for area | <c-area> | apply*record-smem-new-gateway*retrieve |)
###(write (crlf) | Store area: | <c-area> | neighbor | <cp> |, gateway: | <gateway>)
   (<gateway> ^connected no -
                         yes)
   (<c-area> ^neighbor-area <cp>)
   (<cp> ^neighbor-area <c-area>)
   (<cmd> ^store <c-area> <cp> <gateway>)}

sp {apply*record-smem-new-gateway*retrieve*fail*room
   (state <s> ^operator <o>
              ^current-location <c-area>
              ^smem <smem>)
   (<smem> ^command <cmd>
           ^result <res>)
   (<cmd> ^query <q>)
   (<res> ^failure <q>)
   (<o> ^name record-smem-new-gateway-area
        ^gateway <gateway>)
   (<gateway> ^to <to>
              ^x <x> ^y <y>)
   (<c-area> ^type room
             ^id <cid>)
-->
###(write (crlf) |New gateway - create neighbor-area: | <cp> | id | <to> | for area | <c-area> | apply*record-smem-new-gateway*retrieve*fail*room |)
###(write (crlf) | Store area: | <c-area> | neighbor | <cp> |, fail gateway: | <gateway>)
   (<gateway> ^connected no -
                         yes)
   (<c-area> ^neighbor-area <cp>)
   (<cp> ^neighbor-area <c-area>
         ^type door
         ^cleaned yes 
         ^visited no
         ^gateway-x <x>
         ^gateway-y <y>
         ^id <to>)
   (<cmd> ^store <c-area> <cp> <gateway>)}


sp {apply*record-smem-new-gateway*retrieve*fail*door
  #interrupt
   (state <s> ^operator <o>
              ^current-location <c-area>
              ^smem <smem>)
   (<smem> ^command <cmd>
           ^result <res>)
   (<cmd> ^query <q>)
   (<res> ^failure <q>)
   (<o> ^name record-smem-new-gateway-area
        ^gateway <gateway>)
   (<gateway> ^to <to>)
   (<c-area> ^type door
             ^id <cid>)
-->
###(write (crlf) |New gateway - create neighbor-area: | <cp> | id | <to> | for area | <c-area> | apply*record-smem-new-gateway*retrieve*fail-door |)
###(write (crlf) | Store area: | <c-area> | neighbor | <cp> |, fail door: | <gateway>)
   (<gateway> ^connected no -
                         yes)
   (<c-area> ^neighbor-area <cp>)
   (<cp> ^neighbor-area <c-area>
         ^type room
         ^cleaned no
         ^visited no
         ^id <to>)
   (<cmd> ^store <c-area> <cp> <gateway>)}
