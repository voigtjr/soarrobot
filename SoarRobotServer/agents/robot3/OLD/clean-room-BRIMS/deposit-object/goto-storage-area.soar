sp {deposit-object*propose*goto-storage-area
   (state <s> ^name deposit-object
              ^top-state <ts>)
   (<ts> ^storage-area <area>
         ^current-location.id <> <id>)
   (<area> ^id <id>)
-->
###(write (crlf) |Going to found storage area: | <area> |: | <id>)
   (<s> ^operator <op> + =, >)
   (<op> ^name goto-storage-area
         ^type go-to-area
         ^area <area>)
}

sp {deposit-object*propose*find-storage-area
   (state <s> ^name deposit-object
              ^top-state <ts>)
   (<ts> -^storage-area
          ^parameters.areas-held-in wm
          ^current-location { <> <area> <ca> }
          ^areas.area <area>)
   (<area> -^visited yes
            ^id <nid> )
-->
###(write (crlf) |Searching for storage area via: | <nid>)
   (<s> ^operator <op> + =)
   (<op> ^name goto-storage-area
         ^type go-to-area
         ^area <area>)
}

sp {deposit-object*propose*not-saving-areas
   (state <s> ^name deposit-object
              ^top-state <ts>)
   (<ts> -^storage-area
          ^parameters.delete-old-areas true
          ^current-location.gateway <gw>)
-->
###(write (crlf) |Searching for storage area|)
   (<s> ^operator <op> + =)
   (<op> ^name go-to
         ^destination <gw>
         ^cross-doorway no)
}

sp {apply*find-storage-area*destination*area
   (state <s> ^operator <o>
              ^top-state <ts>)
   (<o> ^name goto-storage-area
        ^area <area>)
   (<ts> ^storage-area <area>
        -^destination-area <area>)
-->
   (<ts> ^destination-area <area>)}


############SMEM

sp {deposit-object*propose*goto-storage-area*unvisited*smem*neighbor-area
  #interrupt
   (state <s> ^name deposit-object
              ^top-state.current-location <cur>
             -^top-state.storage-area
              ^top-state.parameters.areas-held-in smem)
   (<cur> ^neighbor-area <area>)
   (<area> -^visited yes
           -^avoid true
            ^id <id>)
-->
   (<s> ^operator <op> + =, <)
   (<op> ^name goto-storage-area
         ^type go-to-area
         ^area <area>)
}
sp {deposit-object*propose*find-storage-area*smem
   (state <s> ^name deposit-object
              ^top-state <ts>)
   (<ts> -^storage-area
          ^parameters.areas-held-in smem)
-->
   (<s> ^operator <op> + =)
   (<op> ^name goto-storage-area
         ^type go-to-area)
}

sp {apply*find-storage-area*smem*query
   (state <s> ^operator <o>
             -^top-state.storage-area
              ^top-state.parameters.areas-held-in smem
              ^smem.command <cmd>)
   (<o> ^name goto-storage-area
       -^area)
-->
   (<cmd> ^query <q>)
   (<q> ^visited no)}

sp {apply*find-storage-area*smem*result
   (state <s> ^operator <o>
              ^smem <smem>)
   (<o> ^name goto-storage-area)
   (<smem> ^command <cmd>
           ^result <res>)
   (<cmd> ^query <q>)
   (<res> ^retrieved <cp>
          ^success <q>)
-->
###(write (crlf) |Retrieved not visited area | <cp> )
   (<s> ^area <cp>)}

sp {apply*find-storage-area*smem*operator
   (state <s> ^operator <o>
              ^area <area>)
   (<o> ^name goto-storage-area)
-->
###(write (crlf) |Retrieved area | <area> )
   (<o> ^area <area>)}


sp {deposit-object*compare*goto-unvisited-area*with*area
   (state <s> ^name deposit-object
              ^top-state.parameters.areas-held-in smem
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name goto-storage-area
          ^area)
   (<op2> ^name goto-storage-area
         -^area)
-->
   (<s> ^operator <op1> > <op2>)
}

